= Expressions
James Elliott <james@deepsymmetry.org>
:icons: font
:toc:
:experimental:
:toc-placement: preamble
:guide-top: v4

// Set up support for relative links on GitHub, and give it
// usable icons for admonitions, w00t! Add more conditions
// if you need to support other environments and extensions.
ifdef::env-github[]
:outfilesuffix: .adoc
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::env-github[]

// Render section header anchors in a GitHub-compatible way when
// building the embedded user guide.
ifndef::env-github[]
:idprefix:
:idseparator: -
endif::env-github[]

Expressions allow you to customize and extend Beat Link Trigger by
writing code in Clojure, the language used to implement the program
itself.

== Overview

A great deal of the power of Beat Link Trigger is in what you can do
with custom expressions. The user interface lets you configure
triggers to send simple MIDI messages in response to basic state
changes, but with expressions you can write your own logic to
determine when the trigger should be enabled, using all of the
information provided by the DJ Link protocol, and you can even send
other kinds of messages locally or over the network to integrate with
systems that do not respond to MIDI.

NOTE: Expressions are written in http://clojure.org[Clojure], the same
language that was used to write Beat Link Trigger. When you write one,
it gets compiled to Java byte-code and loaded, essentially becoming
part of Beat Link Trigger as if it had been there all along. So you
have access to the full power of the language and Java environment,
and can do anything you need to. But you will need to learn a little
Clojure to do it. The official
https://clojure.org/guides/learn/syntax[Learn Clojure] page is a good
starting point, and there are many
https://clojure.org/community/resources[Resources] and
https://clojure.org/community/books[Books] if you decide you want to
go deep.


https://clojure.org/guides/getting_started[Getting Started] page has
some good pointers and links to resources for various learning styles.

The expression you are first likely to want to use is the Enabled
Filter, which is found in each trigger's context menu. In fact, as
soon as you set a trigger's Enabled menu to Custom, Beat Link Trigger
will pop open the editor for that expression for you, if you have not
yet created it:

image:assets/Editor.png[Enabled Filter Editor, 880]

The editor window for each type of expression provides documentation
about the purpose of the expression and guidance on how to write it.
Most expression types have a variety of values that are available to
help you in writing the expression, and those are described and
explained in the help section as well.

Reading through the documentation, we see that the Enabled filter
should return a `true` value to enable the filter, and has access to a
lot of information about the watched player that it can use to decide.
Suppose we want the trigger to be enabled when the player has track
number 5 loaded, and has reached the fourth bar of music (which starts
on beat 17, since there are four beats per bar, and the track starts
with beat 1). With the help of
https://clojure.org/guides/learn/syntax[Learn Clojure] and the help
text in the editor window, we come up with the following expression:

image:assets/EnabledExample.png[Enabled Example, 609]

Try entering that as the Enabled Filter expression for your filter,
set the Enabled menu to Custom, and watch the Trigger Status indicator
as you load and play different tracks to confirm that it works!

TIP: As you use the editor, you will notice that it provides syntax
coloring and parenthesis-matching help. But if you end up making a
mistake in your expression, Beat Link Trigger will likely report an
compilation error when you click kbd:[Update], and you can check the
<<logs,log>> for a more detailed stack trace. You can try googling for
information about the error, or looking in the Clojure
http://clojuredocs.org[documentation], but you can also ask for help
in the Beat Link Trigger
https://gitter.im/brunchboy/beat-link-trigger[Gitter chat room].

== Global Expressions

The Triggers menu at the top of the window lets you define two
expressions that can manage values you want to make available to all
your other expressions. To make that convenient, all expressions have
access to a Clojure http://clojure.org/reference/atoms[atom] named
`globals` that is shared with all other expressions, so that is a
great place to put things for them to find. The atom starts out
holding an empty
http://clojure.org/reference/data_structures#Maps[map], which allows
you to add key/value pairs to organize the information you want to
share across expressions.

=== Global Setup Expression

This is run when Beat Link Trigger starts
up, or when you open a new Trigger file, so it runs before any of your
individual trigger expressions. Here is a fairly sophisticated example that
creates a
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramSocket.html[`DatagramSocket`]
for sending a remote trigger command to the ChamSys MagicQ lighting
control software using its
https://secure.chamsys.co.uk/help/documentation/magicq/ch31.html[remote
protocol]:

```clojure
(let [chamsys-address (InetSocketAddress.
                       (InetAddress/getByName "172.16.42.255") 6553)
      trigger-on (byte-array (map int "71,1H"))
      trigger-off (byte-array (map int "71,0H"))]
  (swap! globals assoc
         :chamsys-socket (DatagramSocket.)
         :chamsys-on (DatagramPacket. trigger-on (count trigger-on)
                                      chamsys-address)
         :chamsys-off (DatagramPacket. trigger-off (count trigger-off)
                                      chamsys-address)))
```

This begins with a `let` binding which sets up some values that will
be used later in the expression. `chamsys-address` gets set to a
https://docs.oracle.com/javase/8/docs/api/java/net/SocketAddress.html[`SocketAddress`]
representing port 6553 (the default port used by MagicQ) on the
broadcast address for the Deep Symmetry show network (you will need to
use the value appropriate for your own network). `trigger-on` and
`trigger-off` are arrays of bytes containing the characters that make
up the commands for turning a MagicQ remote programming trigger on and
off.

Those values are then used in the http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/swap![`swap!`]
call, which is the way you modify a Clojure atom. In this case we are modifying the map in `globals` by
using http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/assoc[`assoc`]
to add some new key-value pairs to it: `:chamsys-socket`
gets associated with a newly-allocated
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramSocket.html[`DatagramSocket`]
that triggers will be able to use for sending UDP messages to MagicQ,
and the actual command packets are created as
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramPacket.html[`DatagramPacket`]
objects preconfigured with the correct command bytes and destination
address and port, in `:chamsys-on` and `:chamsys-off`.

The Beat Expression below shows how these globals are actually used.

=== Global Shutdown Expression

This is run when Beat Link Trigger is
exiting, or before it opens a new Trigger file (or when you choose to
delete all triggers). It gives you a chance to close any connections
and release any system resources that you allocated in your Global
Setup Expression. Here is how we would do that for the ChamSys MagicQ
example we started above:

```clojure
(.close (:chamsys-socket @globals))
```

This simply looks up the
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramSocket.html[`DatagramSocket`]
that was created in the setup expression, and closes it. There is no
need to remove the key/value pairs themselves from the `globals` atom
because Beat Link Trigger will reset it to hold an empty map once the
shutdown expression finishes.

== Trigger Expressions

Each trigger has its own set of expressions which can be accessed from
its context menu. In addition to the `globals` atom described above,
these have access to a very similar `locals` atom which can be used to
share values across expressions within the trigger itself (but not
other triggers; each gets its own `locals` map), and individual
expressions will have other values available to them which make sense
in the context in which the expression is used. The help text below
the expression editor will list and explain the values that are
available for each expression.

=== Setup Expression

This is like the Global Setup Expression described
above, but it is used to set up the `locals` atom which is shared only
with other expressions on the same trigger. It is called when the
trigger is loaded, and when Beat Link Trigger starts up, after the
Global Setup Expression.

For a different example than the Global version, suppose you have a
single trigger that wants to send pitch information to
https://resolume.com[Resolume] Arena 5 so that a clip you are
triggering runs at the same speed as the track playing on the CDJ.
Beat Link Trigger embeds Project Overtone's
https://github.com/rosejn/osc-clj[osc-clj] library and aliases it to
`osc` within the context of expressions to make it easy to send Open
Sound Control messages. Assuming your copy of Arena 5 is running on
the same machine, and listening for OSC messages on port 9801, here is
how you could set things up so your other expressions on this trigger
can communicate with it:

```clojure
(swap! locals assoc :resolume (osc/osc-client "localhost" 9801))
```

This uses
http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/swap![`swap!`]
to modify the map in `locals` by using
http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/assoc[`assoc`]
to add the key `:resolume`, which will hold an OSC client that can be
used to send Open Sound Control messages to Arena 5 on the local
machine. See the Enabled Filter Expression below for how we use it.

=== Shutdown Expression

This is used to release any system resources
(open connections or files) that were allocated by the Setup
Expression. It is called when the trigger is deleted, and when Beat
Link Trigger is exiting, before the Global Shutdown Expression.

Continuing our example, here is how we would clean up the OSC client
we created to talk to Resolume:

```clojure
(osc/osc-close (:resolume @locals))
```

=== Enabled Filter Expression

As described in the
<<overview,introduction>> to this section, this is used when you
set a trigger's Enabled menu to Custom. It is called whenever a status
update packet is received from a watched player, and tells Beat Link
Trigger if the trigger should be enabled or not. Often you will want a
trigger to be enabled when a DJ has loaded a particular track, and a
variety of strategies for achieving that are described in their own
<<matching-tracks,section below>>, see that for lots of great ideas.
Following some of its suggestions, our Resolume example could enable
its trigger with a custom Enabled Filter along the lines of:

```clojure
(= track-title "Language")
```

Since this expression is called every time we get a status update from
a watched player, you might think it could be useful even when you
don't need a custom Enabled state for the trigger, to relay ongoing
state information to other systems like Resolume. But because it is
called to decide which player to track when your trigger is set to
watch Any Player, it will be called more times than you might expect,
so there is a better expression to use for that kind of integration:

=== Tracked Update Expression

This is similar to the Enabled Filter
Expression, but even when a trigger is configured to potentially watch
multiple players, it is called only for the player that is currently
being tracked, which will be the one that is considered “best” as
described in the <<watch-menu,Watch Menu>> section above. Players which
enable the trigger are better than ones that don't; within that group,
it is better to be playing, and as a tie-breaker the lowest numbered
player is chosen.

TIP: The Tracked Update expression is the ideal place to adjust the
track description displayed in the Player Status section of the
trigger by storing values in the `:track-description` and/or
`:metadata-summary` keys of the trigger locals.

Continuing our example, we can use a Tracked Update Expression to
update the playback speed within Arena 5 to stay synced with the
current tempo of the CDJ. We want to send messages to Resolume only
when the trigger is active--which means it is enabled and the player
it is watching is currently playing--so we wrap our expression in a
`when` clause like this:

```clojure
(when trigger-active?
  (let [pitch (/ (- pitch-multiplier 0.05) 2)]
    (osc/osc-send (:resolume @locals) "/activeclip/audio/pitch/values" pitch))))
```

We need to do a little bit of silly math because Beat Link Trigger
represents the current pitch multiplier in a fairly straightforward
way (a range where 0.0 means stopped, 1.0 means normal speed, and 2.0
means double time), while Resolume squashes that whole range into 0.0
to 1.0, slightly off-center. But with that calculation accomplished,
we can simply send the appropriate OSC message to tell it the speed at
which it should be playing. (The OSC path was found by Editing the OSC
Applicaton Map within Arena 5 and clicking on the parameter I wanted
to control, as described in the
https://resolume.com/manual/en/r4/controlling#open_sound_control_osc[manual].)

There is one more improvement we can make, though. Our code as it
stands sends an OSC message to Resolume for every status packet from
the watched player, even when the pitch is not changing. That's
inefficient; it puts needless traffic on the network, and makes
Resolume waste time processing messages that don't change anything. By
adding a little more sophistication to our Tracked Update Expression,
we can keep track of the last value we sent to Resolume, and only send
a new one when it is different. We will use a local named
`:resolume-pitch` to keep track of the last value we sent:

```clojure
(when trigger-active?
  (let [pitch (/ (- pitch-multiplier 0.05) 2)]
    (swap! locals update-in [:resolume-pitch]
           (fn [old-pitch]
             (when (not= pitch old-pitch)
               (osc/osc-send (:resolume @locals) "/layer3/clip3/audio/pitch/values" pitch))
             pitch))))
```

So once again we are only doing anything when the trigger is active
(that's what the `when` clause ensures). Within that, we
compare the current calculated pitch value we want Resolume to be
using with the value that was found in the `locals` map under
`:resolume-pitch`. Only if they are different does `osc-send` get
called to notify Resolume of the new value. Then we store the
calculated value at `:resolume-pitch` so that it is available for
comparison when we get the next status update. The first time this
runs, there will be no comparison value found in `locals`, so we will
always send an initial pitch message to Resolume when the right track
loads for the first time.

If you want to watch this happening, you can add a log statement that
will report the new pitch value each time it is sent, like this:

```clojure
(when trigger-active?
  (let [pitch (/ (- pitch-multiplier 0.05) 2)]
    (swap! locals update-in [:resolume-pitch]
           (fn [old-pitch]
             (when (not= pitch old-pitch)
               (timbre/info "New pitch:" pitch)
               (osc/osc-send (:resolume @locals) "/layer3/clip3/audio/pitch/values" pitch))
             pitch))))
```

With this expression in place, when the trigger is active and you
fiddle with the Pitch fader on the CDJ that is playing the track, you
will see entries like this in the <<logs,log file>>:

```
2016-Jul-24 23:21:31 INFO [beat-link-trigger.expressions:?] - New pitch: 0.475
2016-Jul-24 23:22:18 INFO [beat-link-trigger.expressions:?] - New pitch: 0.4782496452331543
2016-Jul-24 23:22:18 INFO [beat-link-trigger.expressions:?] - New pitch: 0.4802499771118164
```

=== Activation Expression

This is called when the trigger trips (in
other words, when it would send a MIDI message reporting that its
watched player has started to play). You can send additional MIDI
messages here, or use the Clojure and Java networking infrastructure
to send a different kind of message entirely. If this is all you want
the trigger to do, you can set its Message menu to Custom, to suppress
the default MIDI messages that it would otherwise send.

Continuing our Resolume example, here is an Activation expression that
would use OSC to trigger the clip that our Tracked Update expression
was adjusting the pitch for:

```clojure
(osc/osc-send (:resolume @locals) "/layer3/clip3/connect/" (int 1))
```

You can also use the Activation expression to send MIDI messages that
differ from the ones available through the graphical interface. Beat
Link Trigger embeds Project Overtone's
https://github.com/rosejn/midi-clj[midi-clj] library and aliases it to
`midi` within the context of expressions to make it easy to send MIDI
messages. The trigger's chosen MIDI output is available as
`trigger-output` (but may be `nil` if the device is currently not
available). So as an example of how you could send a Note On message
with velocity 42 on the note and channel chosen in the trigger window:

```clojure
(when trigger-output
  (midi/midi-note-on trigger-output trigger-note 42 (dec trigger-channel)))
```

Note that the user-oriented channel number displayed in the Trigger's
Channel menu is actually one larger than the value you actually need
to send in the MIDI protocol (Channel 1 is represented in protocol by
the number 0, and Channel 16 by the number 15, so that the channel can
fit into four bits). So you need to decrement the value of
`trigger-channel` before passing it to the midi library, as shown
above.


=== Deactivation Expression

This is called when the player that the
trigger is watching stops playing, or when the trigger becomes
disabled if it had been active. (This is when a Note Off message, or
Control Change with value zero, is sent.) You can send your own custom
messages here, much like the Activation Expression.

=== Beat Expression

This is called when any of the watched players
reports the start of a new beat. Continuing the example started in the
Global Setup Expression, here is how you could synchronize the BPM of
your ChamSys MagicQ console to the beats coming from your CDJs. Set
the trigger to watch Any Player, and then within the Beat expression,
we will react only to beat packets from the mixer, since it will
always track the master player. (Alternately, you could set the
trigger to watch the Master Player, but this shows an example of how
to filter beat packets by the player number of device sending them,
and that you get beat packets from the mixer itself too.)

```clojure
(when (= device-number 33)
  (.send (:chamsys-socket @locals) (:chamsys-on @locals))
  (future
    (Thread/sleep (long (/ 30000 effective-tempo)))
    (.send (:chamsys-socket @locals) (:chamsys-off @locals))))
```

After checking that the packet came from the mixer (both the DJM 900
nexus and the DJM 2000 nexus identify themselves as player number 33),
we immediately send the UDP packet that tells MagicQ that the remote
trigger is on. We want to later tell it that it is off, but it is
critical that Beat Link Trigger expressions finish and return
promptly, or they will back up the whole event distribution system,
and cause other events to be delayed or lost. So we use Clojure's
http://clojuredocs.org/clojure.core/future[`future`] to send a block
of code to be executed in the background on another thread. The
expression will return immediately, but in the background our inner
block of code sleeps for half a beat (we calculate that by dividing
30,000 milliseconds, or half a minute, by the number of beats per
minute that the mixer reported it is running at). When we wake up,
halfway through the beat, we send the other UDP message that tells
MagicQ the remote trigger is off again. So, by cycling those messages
once per beat, the lighting console can be driven at the same BPM as
the CDJs.

== Learning More

****

* Continue to <<Matching#matching-tracks,Matching Tracks>>
* Return to <<{guide-top}#beat-link-trigger-user-guide,Top>>

****

// Once Git finally supports it, change this to: include::Footer.adoc[]
== License

+++<a href="http://deepsymmetry.org"><img src="assets/DS-logo-bw-200-padded-left.png" align="right" alt="Deep Symmetry logo"></a>+++
Copyright © 2016&ndash;2018 http://deepsymmetry.org[Deep Symmetry, LLC]

Distributed under the
http://opensource.org/licenses/eclipse-1.0.php[Eclipse Public License
1.0], the same as Clojure. By using this software in any fashion, you
are agreeing to be bound by the terms of this license. You must not
remove this notice, or any other, from this software. A copy of the
license can be found in
https://github.com/brunchboy/beat-link-trigger/blob/master/LICENSE[LICENSE]
within this project.
