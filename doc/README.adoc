= Beat Link Trigger User Guide
James Elliott <james@deepsymmetry.org>
:icons: font
:toc:
:experimental:
:toc-placement: preamble

// Set up support for relative links on GitHub; add more conditions
// if you need to support other environments and extensions.
ifdef::env-github[:outfilesuffix: .adoc]

This is an introduction to the Beat Link Trigger interface and
expression programming environment. It shows some ways you can use it,
but as an open-ended environment, the possibilities are endless. If you
have questions or other approaches you'd like to share, please post to
the https://gitter.im/brunchboy/beat-link-trigger[Gitter chat room].

== Startup

When you first open Beat Link Trigger, it tries to find the Pro DJ
Link devices that you want it to monitor. If it can't find any, after
ten seconds you will see this window:

image:assets/NoDevices.png[No devices found]

If you were expecting to find devices, double-check that they are
powered on, and properly networked with your computer (and that any
hub or wireless router you are using is also properly turned on and
configured). Then click kbd:[Try Again] to try again.

If you just wanted to work on setting up your triggers or expressions,
and don't have any actual players handy, you can also click
kbd:[Continue Offline] to work in an offline mode. Your triggers won't
actually fire, but you can do all the configuration and editing that
you need.

Once a device is found, or you choose to continue offline, the Trigger
window opens, in the same configuration that you left it the last time
you used the program.

== Triggers

Each row in the Trigger window defines a single trigger, which watches
a player or category of players, and can be configured to send
different messages in response to what the player is doing.

If this is the first time you have opened Beat Link Trigger, you will
see just one trigger containing default options.

image:assets/FirstTrigger.png[First Trigger, 848]

To create additional triggers you can choose `New Trigger` in the
`Triggers` menu, or type kbd:[⌘+T] (on the Mac) or kbd:[Ctrl+T] (on
other platforms). You can get back to this default state at any time
by choosing `Clear Triggers` in the `Triggers` menu.

image:assets/TriggersMenu.png[Triggers Menu, 341]

Here are the sections of a trigger, and how they work:

=== Comment

The Comment field is a free-form text area you can use to describe the
purpose of the trigger, to remind you when you later come back to it.
Apart from the trigger index, a number by which Beat Link Trigger
identifies the trigger when it is reporting errors or naming related
windows (like expression editors, described <<expressions,below>>),
the comment takes up the entire width of the first line of the trigger
row.

=== Watch Menu

The Watch menu tells the trigger what player(s) it should pay
attention to. It lets you choose Player&nbsp;1 through Player&nbsp;4,
as well as two dynamic choices.

image:assets/WatchMenu.png[Watch Menu, 816]

NOTE: If a player is chosen which is not currently present on the
network, the player will be reported “not found”, and the trigger will
not operate until either the player reappears, or a different player
is chosen. Of course when you are working in Offline mode, all players
will be missing and the trigger will simply say “Offline”.

Any Player:: If you choose to watch Any Player, then just as you would
expect, the trigger will respond to activity on any player in the
network. It will track whatever player seems “best” at the moment: If
there is an Enabled condition which causes the trigger to be enabled
for some players and not for others, it will watch the players that
enable it. Within that group, if some players are playing and others
are not, it will watch the ones that are playing. Finally, if there
are still multiple players to choose between, it will pick the
lowest-numbered one.

Master Player:: If you choose to watch the Master Player, the trigger
will focus on whichever player is the current Tempo (sync) Master.

=== Player Status

Right after the Watch menu, the trigger row displays the player number
and latest status received from the watched player, if one was found.
The status includes the current state of the player, information about
the loaded track, the current effective BPM and relative pitch, and
the number of the current beat.

[NOTE]
====
If a rekordbox-analyzed track was loaded, the database ID of the
track will be displayed, as shown in the figure above, followed by an
indication in square brackets of the player and slot (USB or SD) from
which the track was loaded. That combination (ID number, source player
and slot) will be unique to that track until that media is unloaded
and replaced. IDs are not unique across players and slots.

If another kind of track is loaded (such as an audio CD), instead of
an ID number, its position within its playlist will be shown,
indicated by "#", as shown in the Midi Output Menu figure below.
====

The track ID (along with its source player and slot, if you want to be
safe when you are working with multiple media) can be used to identify
the track and set up fairly sophisticated custom filter expressions,
as described <<matching-tracks,below>>. If one of your expressions
recognizes a track, it can tell Beat Link Trigger to display its name
(or any other text you like) instead of the normal track description
by storing your desired description string under the key
`:track-description` in the trigger `locals` map. Here is an example
of what that could look like, when a track named Apex has been
recognized, and the string `Apex` has been stored under
`:track-description` in the trigger's `locals`:

image:assets/ApexRecognized.png[Track Apex Recognized, 804]

=== MIDI Output Menu

The MIDI Output Menu lets you choose the MIDI device to which the
trigger will send messages. It will show all MIDI outputs currently
available on your system.

image:assets/MidiMenu.png[MIDI Output Menu, 810]

NOTE: If a MIDI Output is chosen which is no longer available on the
system (as shown in trigger 3 above), it will remain in that row's
menu, but the Enabled section of the trigger will be replaced with the
message “Not found.” Once the output reappears, or a different output
is chosen, the trigger will become operational again.

=== Message Menu

The Message Menu determines what kind of MIDI message is sent by the
trigger.

image:assets/MessageMenu.png[Message Menu, 814]

Note:: With this setting, the trigger sends a Note On message, with
velocity 127 and the note number shown to the right of the menu, when
the watched player starts playing (as long as the trigger is enabled),
and a Note Off message when the watched player stops or the trigger is
disabled.

CC:: With this setting, the trigger sends a Control Change message,
with value 127 and the controller number shown to the right of the
menu, when the watched player starts playing (as long as the trigger
is enabled), and sends a CC with value 0 when the player stops or the
trigger is disabled.

Clock:: With this setting, the trigger sends MIDI Beat Clock messages
whenever the trigger is enabled, to synchronize the tempo of the
target device with the tempo reported by the watched player. If the
check box to the right of the menu is checked, it will either send a
Start or Continue message (as chosen in the following menu) when the
watched player starts playing, and if the Stop box is checked, it will
send a Stop message when the watched player stops.

Custom:: When this option is chosen, the trigger will not send any
messages on its own, and it is up to you to send them in code you
write in the trigger expressions, as described <<expressions,below>>.
This gives you the most flexibility because in addition to MIDI
messages, you can send arbitrary UDP packets, HTTP requests, or
whatever you might need.

=== Note/Controller Spinner

Found immediately to the right of the Message Menu (unless MIDI Beat
Clock is chosen as the Message type), this field lets you set the MIDI
note number used for Note messages, or the controller number used for
Control Change messages. The value is also available to your trigger
expressions if they want to use it.

=== Channel Spinner

For triggers sending anything but MIDI Beat Clock, this lets you
specify the MIDI channel on which messages are sent.

=== Enabled Menu

The Enabled menu controls when the trigger will respond to the watched
player starting or stopping playback.

image:assets/EnabledMenu.png[Enabled Menu, 814]

Always:: With this setting, the trigger is enabled until you disable
it.

Never:: With this setting, the trigger is disabled until you re-enable
it.

On-Air:: With this setting, the trigger is enabled whenever the
watched player reports that it is On the Air. (For that to work, the
player must be connected to a Nexus mixer, and must have the feature
turned on.)

Custom:: With this setting, the trigger is controlled by an Enabled
Filter expression that you write yourself. Whenever a status update is
received from any watched player, your expression is called. If it
returns a `true` value, the trigger will be enabled. This lets you
apply sophisticated logic, like enabling the trigger when a particular
track number is loaded into the player, and it has reached a
particular beat during playback. Expressions are further explained
<<expressions,below>>. If you choose Custom and have not yet written
an Enabled Filter expression, the expression editor will be opened to
let you do that.

=== Trigger Status

To the right of the Enabled menu there is a Trigger Status indicator
which shows whether the trigger is currently enabled (a green circle)
or disabled (a red circle with a slash). If the player is currently
playing, there is a filled circle inside the enabled circle:

[width="50%",cols=".^,^.^",options="header"]
|===
|State
|Indicator

|Disabled, Not Playing
|image:assets/Disabled.png[Disabled]

|Enabled, Not Playing
|image:assets/Enabled.png[Enabled]

|Disabled, Playing
|image:assets/DisabledPlaying.png[Disabled, Playing]

|Enabled, Playing
|image:assets/EnabledPlaying.png[Enabled, Playing]

|===

=== Context Menu

Each trigger row has a context menu attached to it, which can be
accessed by right-clicking (or control-clicking) anywhere on the row's
background, but you can also open the context menu with a regular
mouse click on the button with a gear icon in it. Most of the menu is
devoted to editing various expressions to customize the trigger, as
described <<expressions,below>>. The gear icon next to an expression
will be filled in if that expression has a value, and the gear in the
button will be filled in if any expression associated with the trigger
has a value.

image:assets/ContextMenu.png[Context Menu, 254]

Below the expression-related options, you can export the current
trigger configuration to a text file which can be imported into a
different trigger or shared with colleagues, and you can delete the
trigger, unless it is the only remaining trigger in the window.

== Expressions

A great deal of the power of Beat Link Trigger is in what you can do
with custom expressions. The user interface lets you configure
triggers to send simple MIDI messages in response to basic state
changes, but with expressions you can write your own logic to
determine when the trigger should be enabled, using all of the
information provided by the DJ Link protocol, and you can even send
other kinds of messages locally or over the network to integrate with
systems that do not respond to MIDI.

NOTE: Expressions are written in http://clojure.org[Clojure], the same
language that was used to write Beat Link Trigger. When you write one,
it gets compiled to Java byte-code and loaded, essentially becoming
part of Beat Link Trigger as if it had been there all along. So you
have access to the full power of the language and Java environment,
and can do anything you need to. But you will need to learn a little
Clojure to do it. http://www.tryclj.com[Try Clojure] is a five-minute
online introduction that is a great starting point. Please give it a
try!

The expression you are first likely to want to use is the Enabled
Filter, which is found in each trigger's context menu. In fact, as
soon as you set a trigger's Enabled menu to Custom, Beat Link Triggger
will pop open the editor for that expression for you, if you have not
yet created it:

image:assets/Editor.png[Enabled Filter Editor, 880]

The editor window for each type of expression provides documentation
about the purpose of the expression and guidance on how to write it.
Most expression types have a variety of values that are available to
help you in writing the expression, and those are described and
explained in the help section as well.

Reading through the documentation, we see that the Enabled filter
should return a `true` value to enable the filter, and has access to a
lot of information about the watched player that it can use to decide.
Suppose we want the trigger to be enabled when the player has track
number 5 loaded, and has reached the fourth bar of music (which starts
on beat 17, since there are four beats per bar, and the track starts
with beat 1). Armed with the Clojure knowledge gained from our
http://www.tryclj.com[Try Clojure] detour, and the help text in the
editor window, we come up with the following expression:

image:assets/EnabledExample.png[Enabled Example, 609]

Try entering that as the Enabled Filter expression for your filter,
set the Enabled menu to Custom, and watch the Trigger Status indicator
as you load and play different tracks to confirm that it works!

TIP: As you use the editor, you will notice that it provides syntax
coloring and parenthesis-matching help. But if you end up making a
mistake in your expression, Beat Link Trigger will likely report an
compilation error when you click kbd:[Update], and you can check the
<<logs,log>> for a more detailed stack trace. You can try googling for
information about the error, or looking in the Clojure
http://clojuredocs.org[documentation], but you can also ask for help
in the Beat Link Trigger
https://gitter.im/brunchboy/beat-link-trigger[Gitter chat room].

=== Global Expressions

The Triggers menu at the top of the window lets you define two
expressions that can manage values you want to make available to all
your other expressions. To make that convenient, all expressions have
access to a Clojure http://clojure.org/reference/atoms[atom] named
`globals` that is shared with all other expressions, so that is a
great place to put things for them to find. The atom starts out
holding an empty
http://clojure.org/reference/data_structures#Maps[map], which allows
you to add key/value pairs to organize the information you want to
share across expressions.

Global Setup Expression:: This is run when Beat Link Trigger starts
up, or when you open a new Trigger file, so it runs before any of your
individual trigger expressions. Here is a fairly sophisticated example that
creates a
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramSocket.html[`DatagramSocket`]
for sending a remote trigger command to the ChamSys MagicQ lighting
control software using its
https://secure.chamsys.co.uk/help/documentation/magicq/ch31.html[remote
protocol]:
+
```clojure
(let [chamsys-address (InetSocketAddress.
                       (InetAddress/getByName "172.16.42.255") 6553)
      trigger-on (byte-array (map int "71,1H"))
      trigger-off (byte-array (map int "71,0H"))]
  (swap! globals assoc
         :chamsys-socket (DatagramSocket.)
         :chamsys-on (DatagramPacket. trigger-on (count trigger-on)
                                      chamsys-address)
         :chamsys-off (DatagramPacket. trigger-off (count trigger-off)
                                      chamsys-address)))
```
+
This begins with a `let` binding which sets up some values that will
be used later in the expression. `chamsys-address` gets set to a
https://docs.oracle.com/javase/8/docs/api/java/net/SocketAddress.html[`SocketAddress`]
representing port 6553 (the default port used by MagicQ) on the
broadcast address for the Deep Symmetry show network (you will need to
use the value appropriate for your own newtork). `trigger-on` and
`trigger-off` are arrays of bytes containing the characters that make
up the commands for turning a MagicQ remote programming trigger on and
off.
+
Those values are then used in the http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/swap![`swap!`]
call, which is the way you modify a Clojure atom. In this case we are modifying the map in `globals` by
using http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/assoc[`assoc`]
to add some new key-value pairs to it: `:chamsys-socket`
gets associated with a newly-allocated
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramSocket.html[`DatagramSocket`]
that triggers will be able to use for sending UDP messages to MagicQ,
and the actual command packets are created as
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramPacket.html[`DatagramPacket`]
objects preconfigured with the correct command bytes and destination
address and port, in `:chamsys-on` and `:chamsys-off`.
+
The Beat Expression below shows how these globals are actually used.

Global Shutdown Expression:: This is run when Beat Link Trigger is
exiting, or before it opens a new Trigger file (or when you choose to
delete all triggers). It gives you a chance to close any connections
and release any system resources that you allocated in your Global
Setup Expression. Here is how we would do that for the ChamSys MagicQ
example we started above:
+

```clojure
(.close (:chamsys-socket @globals))
```
+

This simply looks up the
https://docs.oracle.com/javase/8/docs/api/java/net/DatagramSocket.html[`DatagramSocket`]
that was created in the setup expression, and closes it. There is no
need to remove the key/value pairs themselves from the `globals` atom
because Beat Link Trigger will reset it to hold an empty map once the
shutdown expression finishes.

=== Trigger Expressions

Each trigger has its own set of expressions which can be accessed from
its context menu. In addition to the `globals` atom described above,
these have access to a very similar `locals` atom which can be used to
share values across expressions within the trigger itself (but not
other triggers; each gets its own `locals` map), and individual
expressions will have other values available to them which make sense
in the context in which the expression is used. The help text below
the expression editor will list and explain the values that are
available for each expression.

Setup Expression:: This is like the Global Setup Expression described
above, but it is used to set up the `locals` atom which is shared only
with other expressions on the same trigger. It is called when the
trigger is loaded, and when Beat Link Trigger starts up, after the
Global Setup Expression.
+

For a different example than the Global version, suppose you have a
single trigger that wants to send pitch information to
https://resolume.com[Resolume] Arena 5 so that a clip you are
triggering runs at the same speed as the track playing on the CDJ.
Beat Link Trigger embeds Project Overtone's
https://github.com/rosejn/osc-clj[osc-clj] library and aliases it to
`osc` within the context of expressions to make it easy to send Open
Sound Control messages. Assuming your copy of Arena 5 is running on
the same machine, and listening for OSC messages on port 9801, here is
how you could set things up so your other expressions on this trigger
can communicate with it:
+

```clojure
(swap! locals assoc :resolume (osc/osc-client "localhost" 9801))
```
+

This uses
http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/swap![`swap!`]
to modify the map in `locals` by using
http://clojure.github.io/clojure/clojure.core-api.html#clojure.core/assoc[`assoc`]
to add the key `:resolume`, which will hold an OSC client that can be
used to send Open Sound Control messages to Arena 5 on the local
machine. See the Enabled Filter Expression below for how we use it.

Shutdown Expression:: This is used to release any system resources
(open connections or files) that were allocated by the Setup
Expression. It is called when the trigger is deleted, and when Beat
Link Trigger is exiting, before the Global Shutdown Expression.
+

Continuing our example, here is how we would clean up the OSC client
we created to talk to Resolume:
+

```clojure
(osc/osc-close (:resolume @locals))
```

Enabled Filter Expression:: As described in the introduction to this
section, this is used when you set a trigger's Enabled menu to Custom.
It is called whenever a status update packet is received from a
watched player, and tells Beat Link Trigger if the trigger should be
enabled or not.
+

Since this expression is called every time we get a status update from
a watched player, it can be very useful even if you don't need a
custom Enabled state for the trigger. In our example, we can use it to
update the playback speed within Arena 5 to stay synced with the
current tempo of the CDJ:
+

```clojure
(let [pitch (/ (- pitch-multiplier 0.05) 2)]
  (osc/osc-send (:resolume @locals) "/activeclip/audio/pitch/values" pitch)))
true
```
+

We need to do a little bit of silly math because Beat Link Trigger
represents the current pitch multiplier in a fairly straightforward
way (a range where 0.0 means stopped, 1.0 means normal speed, and 2.0
means double time), while Resolume squashes that whole range into 0.0
to 1.0, slightly off-center. But with that calculation accomplished,
we can simply send the appropriate OSC message to tell it the speed at
which it should be playing. (The OSC path was found by Editing the OSC
Applicaton Map within Arena 5 and clicking on the parameter I wanted
to control, as described in the
https://resolume.com/manual/en/r4/controlling#open_sound_control_osc[manual].)
+

TIP: The `true` on the last line is there because even when we want to
use the Enabled Filter Expression for another purpose like this, it
will _still_ control whether the trigger is enabled or not, and needs
to return a true value or it will disable the filter. (And, as it
turns out, `osc-send` returns `nil`, which is not considered a true
value.)
+

Of course, we may want to still have this trigger enabled only when a
particular track is loaded into the CDJ, and we can do both things in
a single expression. Here is how we would check that the track number
is 8 before sending anything to Resolume, and pass that information
along to Beat Link Trigger so that the trigger itself is only enabled
when that track is loaded:
+

```clojure
(let [result (= track-number 8)
      pitch (/ (- pitch-multiplier 0.05) 2)]
  (when result
    (osc/osc-send (:resolume @locals) "/activeclip/audio/pitch/values" pitch))
  result)
```

Activation Expression:: This is called when the trigger trips (in
other words, when it would send a MIDI message reporting that its
watched player has started to play). You can send additional MIDI
messages here, or use the Clojure and Java networking infrastructure
to send a different kind of message entirely. If this is all you want
the trigger to do, you can set its Message menu to Custom, to suppress
the default MIDI messages that it would otherwise send.
+

Beat Link Trigger embeds Project Overtone's
https://github.com/rosejn/midi-clj[midi-clj] library and aliases it to
`midi` within the context of expressions to make it easy to send MIDI
messages. The trigger's chosen MIDI output is available as
`trigger-output` (but may be `nil` if the device is currently not
available). So as an example of how you could send a Note On message
with velocity 42 on the note and channel chosen in the trigger window:
+

```clojure
(when trigger-output
  (midi/midi-note-on trigger-output trigger-note 42 (dec trigger-channel))
```
+

Note that the user-oriented channel number displayed in the Trigger's
Channel menu is actually one larger than the value you actually need
to send in the MIDI protocol (Channel 1 is represented in protocol by
the number 0, and Channel 16 by the number 15, so that the channel can
fit into four bits). So you need to decrement the value of
`trigger-channel` before passing it to the midi library, as shown
above.


Deactivation Expression:: This is called when the player that the
trigger is watching stops playing, or when the trigger becomes
disabled if it had been active. (This is when a Note Off message, or
Control Change with value zero, is sent.) You can send your own custom
messages here, much like the Activation Expression.

Beat Expression:: This is called when any of the watched players
reports the start of a new beat. Continuing the example started in the
Global Setup Expression, here is how you could synchronize the BPM of
your ChamSys MagicQ console to the beats coming from your CDJs. Set
the trigger to watch Any Player, and then within the Beat expression,
we will react only to beat packets from the mixer, since it will
always track the master player. (Alternately, you could set the
trigger to watch the Master Player, but this shows an example of how
to filter packets by the type of device sending them, and that you get
beat packets from the mixer itself too.)
+

```clojure
(when mixer?
  (.send (:chamsys-socket @locals) (:chamsys-on @locals))
  (future
    (Thread/sleep (long (/ 30000 effective-tempo)))
    (.send (:chamsys-socket @locals) (:chamsys-off @locals))))
```
+

After checking that the packet came from the mixer, we immediately
send the UDP packet that tells MagicQ that the remote trigger is on.
We want to later tell it that it is off, but it is critical that Beat
Link Trigger expressions finish and return promptly, or they will back
up the whole event distribution system, and cause other events to be
delayed or lost. So we use Clojure's
http://clojuredocs.org/clojure.core/future[`future`] to send a block
of code to be executed in the background on another thread. The
expression will return immediately, but in the background our inner
block of code sleeps for half a beat (we calculate that by dividing
30,000 milliseconds, or half a minute, by the number of beats per
minute that the mixer reported it is running at). When we wake up,
halfway through the beat, we send the other UDP message that tells
MagicQ the remote trigger is off again. So, by cycling those messages
once per beat, the lighting console can be driven at the same BPM as
the CDJs.

== Matching Tracks

As described in the <<player-status,Player Status>> discussion, the
most reliable way to match a track is using the `rekordbox-id` value,
which uniquely identifies the track within the media (USB stick or SD
card) from which it was loaded. However, these IDs are not unique
across media--each attached USB stick or SD card will number its
tracks with IDs that start at 1 and increment from there, so as soon
as you have multiple media attached to one or more players, there will
multiple different tracks with the same `rekordbox-id` value. To be
safe you will also need to consider the `track-source-player` value,
which tells you the player from which the track was loaded, and the
`track-source-slot` value, which will be `:sd-slot` for tracks loaded
from SD cards, and `:usb-slot` for tracks loaded from USB drives. For
as long as the same set of media is mounted in the CDJs, the
combination of `track-source-player`, `track-source-slot`, and
`rekordbox-id` will uniquely identify a track.

NOTE: It would be nice to be able to ask the players for metadata
about the track, such as the title and artist, and use that
information for matching. We are working on finding a way to do that
reliably and safely, but even before that effort is successful, this
section will provide some practical ways of achieving results almost
as good. And even if we do solve the metadata-request issues, you may
still prefer to use the approaches described here, because they place
less load on the network and CDJs.

Depending on how many different tracks you want to watch for, and how
differently you want to react to them, there are two different ways to
approach matching them.

=== One Trigger per Track

If you are only dealing with a few tracks, and especially if you want
to do fundamentally different things in response to detecting each
track is being played, this approach might work well. The triggers are
set to Watch Any Player, and the Enabled Filter expression activates
each when any player has loaded the track that the trigger cares
about. For example, in the following screen shot we have two triggers
watching for two specific tracks:

image:assets/MatchTracks.png[Matching Tracks, 870]

The Enabled Filter expression for the first trigger is as follows:

```clojure
(and
  (= rekordbox-id 15) (= track-source-player 3) (= track-source-slot :usb-slot)
  (>= beat-number 225))
```

This activates the trigger whenever a player has loaded the track with
ID 15 from the USB drive attached to player 3, and playback has
reached beat number 255. The Enabled Filter expression for the second
trigger is similar:

```clojure
(and
  (= rekordbox-id 655) (= track-source-player 3) (= track-source-slot :usb-slot)
  (>= beat-number 17))
```

Notice in the screen shot that both triggers are enabled, and watching
different players, but the tracks were both loaded from the USB slot
in player 3, which is exactly what the expressions specified. Also
note that using normal Clojure expressions, you can combine matching
the track with whatever other conditions you care about (in this
example, beat position).

==== Adapting to Changes

Even with a small number of tracks, there is a drawback to the
expressions we are using: If you set them up in advance, and then
during the performance, the DJ needs to put the media into a different
player, you will need to go into each trigger's Enabled Filter
separately and correct the player that it is looking for. This is
tedious and error-prone, and with more than a few triggers, frankly
unmanageable. But there is a better way.

We can use the Global Setup expression to set up global configuration
information that specifies where our DJ has loaded his rekordbox
database, so that there is only one place to update during the
performance once we see where his tracks are being loaded from:

```clojure
(swap! globals assoc :symmetry-media
  {:player 3
   :slot :usb-slot})
```

The USB drive on my keychain is named Symmetry, so this Global Setup
expression adds the key `:symmetry-media` to the Beat Link Trigger
`globals` map, and stores a map there containing the player number 3
and the slot `:usb-slot`. This can then be used in each Enabled Filter
expression, like so:

```clojure
(let [{:keys [player slot]} (:symmetry-media @globals)]
  (and
    (= rekordbox-id 15) (= track-source-player player) (= track-source-slot slot)
    (>= beat-number 225)))
```

This uses Clojure's handy map destructuring feature in the `let` form
to bind the symbols `player` and `slot` to the corresponding values
looked up in the map stored at `:symmetry-media` in the Beat Link
Trigger `globals` atom. It then uses those values instead of the
formerly hard-coded literal values in the expression. Once each
Enabled Filter expression is written this way, as soon as you edit the
Global Setup expression to change the player number or slot, all of the
filter expressions immediately start watching for tracks loaded from
the updated location.

Of course if you have multiple media that you are watching for
different tracks on, you can store the location of each under a
separate key in the globals, and look them up using that key in the
triggers that need to work with tracks from that media.

=== One Trigger per Player

When you have a great many tracks that you want to watch for, managing
so many triggers becomes awkward, even when you use globals to
identify the player and slot where tracks should be loaded from.
Instead, you can take that idea even further, and set up a global map
that describes all the tracks you are interested in, along with
whatever other information you need to react to them. In this
approach, your Enabled Filter Expression will look up the track in the
global map, and when it finds a match, mark the trigger as enabled,
along with recording whatever other information about the track might
be needed to react appropriately in a custom Activation expression.

TIP: Because more than one track which matches the global map might be
loaded at the same time, this approach relies on having you set up an
individual trigger for each player you want to watch, rather than
having the trigger watch Any Player.

So what does this global map of tracks look like? Our new Global Setup
expression is only slightly more elaborate than the one we used in the
previous example to keep track of the player and slot our DJ's
rekordbox database was loaded in:

```clojure
(swap! globals assoc :watched-tracks
  {3  ; The outermost key specifies the player tracks must be loaded from
    {:usb-slot  ; Next key specifies the slot
      ;; And now the map of track IDs and information about each
      {1 {:name "Rainbow (Jack rmx)" :beat-ccs {33 1}}
       2 {:name "Best Day (Gent rmx)" :beat-ccs {17 2 65 3}}
       73  {:name "Azuca (Club mix)" :beat-ccs {1 4}}
       584 {:name "Bubble Control" :beat-ccs {9 5}}
       873 {:name "Climax" :beat-ccs {63 6}}
  }}})
```

We build a series of nested maps. As noted, the outermost key is the
number of a player containing a database holding tracks we are
watching for. This plays the same role as the `:player` key in the
earlier example: a single place to edit when we determine where our DJ
has loaded the media containing the tracks we are watching for. Inside
that is the keyword identifying the slot holding the database of
interest. And inside that comes the main map identifying and
describing the tracks we are watching for.

NOTE: We could have used a variety of structures for organizing this
information. Nested maps have a few advantages. As you'll see in the
Enabled filter source below, it's easy to navigate into them using the
`get-in` function. And this approach lets us keep track of more than
one rekordbox database containing tracks we want to watch for, by
simply adding additional player numbers paired with appropriate nested
content at the right level in the map.
http://www.braveclojure.com/do-things/#Maps[Clojure for the Brave and
True] is one place where you can learn more about Clojure maps.

The map nested after the slot specification key (`:usb-slot` in the
example above) identifies the tracks we are interested in when they
are loaded from that slot. It pairs the rekordbox ID number of each
track with whatever other information we might need to know about that
track. Finding a track's ID in this map after we've navigated down
through the player number and slot keyword means that we are
interested in the track, and the other information we attach to its ID
lets us do some pretty useful things.

In this example, we are tracking a `:name` string for each track, and
another map we store as `:beat-ccs` that will tell us the particular
beats within the track where we want to activate, as well as the MIDI
Controller Change number we want to send to identify the track that's
activating when that beat is reached.

The `:name` entries in the track description maps play a double role.
First, they help us when looking at this expression itself to remember
what track each entry is matching. But the Enabled filter can also use
the name string to show the user what track has been matched. This
didn't matter in the One Trigger per Track approach, because each
trigger had a Comment field where you could enter the track name it
matched. But in this approach, we have only a trigger for each player,
and the trigger will activate whenever the player loads a track that
is listed in the `:watched-tracks` global. So, without memorizing all
the track IDs, how can you tell which one has been matched? Well, as
described <<player-status,above>>, the Enabled filter can tell Beat
Link Trigger to display the name of the track it has matched by
copying the `:name` string to the key `:track-description` in the
trigger `locals` atom. Let's look at the Enabled filter's code now:


```clojure
(if-let [track (get-in @globals [:watched-tracks track-source-player track-source-slot rekordbox-id])]
  (do  ; Recognized track; set the name, enable if on a flagged beat
    (swap! locals assoc :track-description (:name track))
    (when-let [cc (get-in track [:beat-ccs beat-number])]
      (swap! locals assoc :activate-cc cc)))
  (do  ; Unknown track, clear name, return nil to prevent activation
    (swap! locals dissoc :track-description)
    nil))
```

The first part uses `get-in` to navigate through the nested map
structure we created to describe tracks, looking up a value by
starting with the player number from which the track has been loaded,
then the slot from which it was loaded, and finally the rekordbox ID.
If, for example the track was loaded from player `3` in the
`:usb-slot` and the ID was `1`, looking at the `:watched-tracks` map
above, that would set `track` to:

```clojure
{:name "Rainbow (Jack rmx)" :beat-ccs {33 1}}
```

When `track` is successfully bound to a value like this, the `if-let`
form executes the first form in its body, labeled with the
&ldquo;Recognized track&rdquo; comment. That code copies the track
name that was found into the `:track-description` local so that Beat
Link Trigger will display it in the trigger row, then goes on to check
whether the curent beat is one of the keys in the `:beat-ccs` map. If
it is, the following value is copied to the trigger local named
`:activate-cc`, which will be used by the custom Activation expression
below to send the appropriate MIDI CC message, and a non-empty value
is returned, which tells the trigger that it is enabled.

In this particular example, when the beat number is `33`, the trigger
will enable itself and set `:activate-cc` to `1`. If the beat number
has any other value, the track name is still displayed, but the
trigger is disabled.

If any of the `:watched-tracks` key lookups in the first line fails
anywhere along the way (the track ID is not in the map, or perhaps
`track-source-slot` has the value `:no-track` because no track has
even been loaded) then the `if-let` form does not assign a value to
`track`, and it executes the second part of its body (with the
&ldquo;Unknown track&rdquo; comment). That code removes the
`:track-description` local so Beat Link Trigger will display its
normal numerical descripton of the track status, and returns `nil` to
indicate that the filter should not be enabled.

Here's what this set of expressions looks like in action:

image:assets/MatchTracks2.png[Matching Tracks 2, 848]

Notice that as planned, each trigger is configured to watch a single
player. They each have identical copies of the above Enabled filter
installed, and are set to use it, which is why the loaded track names
are showing up in the blue Player Status section. The first trigger is
enabled, because that player is sitting at the beat mentioned in the
track's `:beat-ccs` map. As soon as that player starts playing, the
trigger will activate. But how will it know which controller number it
is supposed to send in its activation message? That's taken care of by
the custom Activation expression that has been installed:

```clojure
(when trigger-output
  (when-let [cc (:activate-cc @locals)]
    (midi/midi-control trigger-output cc 127 (dec trigger-channel))))
```

This expression first checks that the trigger's chosen MIDI Output can
be found (to avoid throwing exceptions trying to send to a missing
device), then looks for the value that the Enabled filter stored in
the `:activate-cc` local. It then sends a MIDI CC message to that
controller number, with the value 127, on the channel chosen by the
trigger. (It calls `dec` because the MIDI protocol actually uses the
numbers 0--15 to refer to the channels described as 1--16.)

In this example, the system being triggered only needs to know when
the track reaches that point, so the enabled filter can disable the
trigger as soon as the next beat is reached, and reactivate with a
different CC when another beat of interest is reached (the Just a Gent
remix of Best Day of my Life in this example sends CC 2 on beat 17,
and CC 3 on beat 65, using `:beat-ccs {17 2 65 3}`).

If we need to send a CC to the same controller with the value 0 when
the trigger deactivates, a very similar Deactivation expression can
be installed:

```clojure
(when trigger-output
  (when-let [cc (:activate-cc @locals)]
    (midi/midi-control trigger-output cc 0 (dec trigger-channel))))
```

And of course if you can use OSC to communicate rather than MIDI, you
are already writing custom Activation and Deactivation expressions,
which is what you need to use for that, and you can send much more
information about the track that way: the name, the actual rekordbox
ID number, or some other value that you add under a new key in the
`:watched-tracks` map. You can structure this as richly as you need.

If you need the trigger to deactivate on specific beats, rather than
always on the beat after it activates, that can be done with only
slightly more code and tracking structures. I will leave it as an
exercise to the reader, but if you get stuck or want to discuss your
approach, please say so in the
https://gitter.im/brunchboy/beat-link-trigger[Gitter chat room].


== Logs

When Beat Link Trigger is running it reports events to a log file. As
noted above, this includes full stack traces for compilation problems
when you try to save an expression that isn't quite right. If a
problem occurs while trying to run your expression later, that will
end up in the log too. So it can be a useful place to look when things
are not working the way you expect. You can find the log file by using
the Open Logs Folder option in the File menu.

Each time you launch Beat Link Trigger it creates a new log folder in
a temporary directory, so that they can be cleaned up automatically
sometime after it exits. It cycles through log files and limits their
maximum length, which can be important if an error is being logged
each time a packet comes in because of a problematic Enabled Filter
expression.

Even if things are not crashing, you might want to log your own
information to get a better understanding of what is happening inside
one of your expressions. Beat Link Trigger uses
https://github.com/ptaoussanis/timbre[timbre] to perform its logging,
and so can you.

For example, if you are trying to figure out exactly what you are
receiving in your Beat expression, you could add this as its first
line:

```clojure
(timbre/info "Received beat" status "master?" tempo-master?)
```

Suddenly your logs will be growing steadily, filling with lines like these:

```
2016-Jun-05 00:12:10 Alacrity.local INFO [beat-link-trigger.expressions:?] -
  Received beat Beat: Device 2, name: CDJ-2000nexus, pitch: +0.00%,
  track BPM: 126.0, effective BPM: 126.0, beat within bar: 4 master? true
2016-Jun-05 00:12:11 Alacrity.local INFO [beat-link-trigger.expressions:?] -
  Received beat Beat: Device 33, name: DJM-2000nexus, pitch: +0.00%,
  track BPM: 126.0, effective BPM: 126.0, beat within bar: 1 master? false
```

== Inspecting Locals and Globals

In addition to logging values to the log file, you can get a glimpse
at what your expressions are up to by opening an inspector window on
the Expression Globals or a particular trigger's Expression Locals.
The first is done by choosing Inspect Expression Globals in the
Triggers menu. It opens a window showing you all the keys and values
that have been created in the `globals` atom shared by all triggers.
Here is what that looks like after the example code shown
<<global-expressions,above>> has run:

image:assets/ExpressionGlobals.png[Enabled Example, 872]

The inspector is a little busy, but right away you can see the three
keys we created, and the corresponding Java objects stored under them.
by clicking next to the blue diamond in the lower pane, you can expand
each entry and dive down into the fields and values that make it up,
which can be quite a powerful way to explore the objects.

Similarly, the locals for a trigger can be inspected by choosing
Inspect Expression Locals from that trigger's context menu. Here's the
result of drilling down a little into the `:resolume` OSC client
object created in that example's trigger:

image:assets/ExpressionLocals.png[Enabled Example, 789]

== Saving and Loading

The entire trigger configuration can be saved to a text file by
choosing Save in the File menu. That file can be sent to another
machine, shared with a colleague, or just kept around for future use
after you are done with a different project. As you would expect, the
Load option replaces the current trigger configuration with one loaded
from a save file.

=== Exporting and Importing Triggers

As mentioned in the <<context-menu,Context Menu>> section, individual
triggers can be exported on their own, and imported into other trigger
configurations.

== What Next?

Hopefully this has been enough to get you started, and thinking about
interesting ways you can synchronize your CDJs with other elements of
your show.

If you have any thoughts, questions, or even crazy ideas, please share
them in the https://gitter.im/brunchboy/beat-link-trigger[Gitter
chat]!

If you find what seems to be an actual problem with the software,
please open an
https://github.com/brunchboy/beat-link-trigger/issues[Issue], or at
least check whether someone else already has.

Thanks for reading this, and have fun with Beat Link Trigger! I hope
to hear from you.

## License

+++<a href="http://deepsymmetry.org"><img src="assets/DS-logo-bw-200-padded-left.png" align="right" alt="Deep Symmetry logo"></a>+++
Copyright © 2016 http://deepsymmetry.org[Deep Symmetry, LLC]

Distributed under the
http://opensource.org/licenses/eclipse-1.0.php[Eclipse Public License
1.0], the same as Clojure. By using this software in any fashion, you
are agreeing to be bound by the terms of this license. You must not
remove this notice, or any other, from this software. A copy of the
license can be found in
https://github.com/brunchboy/beat-link-trigger/blob/master/LICENSE[LICENSE]
within this project.
